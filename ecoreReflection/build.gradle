plugins {
	id "tui.sse.mde4cpp.mde4cpp-compile-plugin" version "0.2"
}
apply plugin: 'base' 

description 'Build tasks for project: EcoreReflection'

task buildEcoreReflection(dependsOn: ['generateEcoreReflection', 'compileEcoreReflection']) {
	group 'EcoreReflection'
	description 'build EcoreReflection'
}

task compileEcoreReflection(type: tui.sse.mde4cpp.MDE4CPPCompile) {
	group 'EcoreReflection'
	description 'compile EcoreReflection'

	projectFolder = file("./src_gen/EcoreReflection")
}

task generateEcoreReflection(type: JavaExec) {
	group 'EcoreReflection'
	description 'generate C++ code of EcoreReflection.uml model'
	main = "-jar"
	workingDir "./model"
	args System.getenv('MDE4CPP_HOME') + "/application/generator/UML4CPP.jar", "EcoreReflection.uml", "../src_gen"
	inputs.files file(System.getenv('MDE4CPP_HOME') + "/application/generator/UML4CPP.jar"), file("model/EcoreReflection.uml")
	outputs.dir file("src_gen")

	doFirst {
		if (System.getenv('MDE4CPP_HOME') == null) {
			throw new GradleException('System environment variable "MDE4CPP_HOME" is not defined')
		}
		if (!file(System.getenv('MDE4CPP_HOME') + "/application/generator/UML4CPP.jar").exists()) {
			throw new GradleException('UML4CPP.jar is missing in folder ' + file(System.getenv('MDE4CPP_HOME') + '/application/generator') + '. Please run task createUML4CPP!')
		}
	}
}

compileEcoreReflection.mustRunAfter generateEcoreReflection

clean.doFirst {
	def cmakeFolder = file('./src_gen/EcoreReflection/.cmake/')
	if(cmakeFolder.exists()) {
    	delete cmakeFolder.absolutePath
   		println "deleting folder " + cmakeFolder.absolutePath
	}
}
