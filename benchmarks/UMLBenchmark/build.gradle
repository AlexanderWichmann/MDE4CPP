apply plugin: 'cpp'

description 'Example project: Benchmark of UML use '

File appendDebugSuffix(File binaryFile) {
  int extensionSeparatorIndex = binaryFile.path.lastIndexOf('.')
  return new File(binaryFile.path.substring(0, extensionSeparatorIndex) + "d" + binaryFile.path.substring(extensionSeparatorIndex))
}

task deliverApplication(dependsOn: ['deliverApplicationDebugExecutable', 'deliverApplicationReleaseExecutable']) {
	description 'deliver debug and release executable to application folder (%MDE4CPP_HOME%/application/bin)'
}

task deliverApplicationDebugExecutable(type: Copy) {
	description 'deliver types debug libary to %MDE4CPP_HOME%/application/bin'
	from "build/exe/uMLBenchmark/debug"
	into System.getenv('MDE4CPP_HOME')+"/application/bin"
	include "UMLBenchmarkd.exe"
}

task deliverApplicationReleaseExecutable(type: Copy) {
	description 'deliver types release libary to %MDE4CPP_HOME%/application/bin'
	from "build/exe/uMLBenchmark/release"
	into System.getenv('MDE4CPP_HOME')+"/application/bin"
	include "UMLBenchmark.exe"
}


model {
    toolChains {
    	 gcc(Gcc) {
    	 	"g++"
    	 }
    }
	
    buildTypes {
        debug 
        release
    }
	repositories {
        libs(PrebuiltLibraries) {
			[ "ecore", "types", "uml" ].each {
				delegate."$it" {
					def libraryName = "${name}"
					headers.srcDir System.getenv('MDE4CPP_HOME')+"/application/include/${libraryName}"
					binaries.withType(SharedLibraryBinary) {
						if(buildType == buildTypes.debug) {
							sharedLibraryFile = file(System.getenv('MDE4CPP_HOME')+"/application/bin/${libraryName}d.dll")
						} else {
							sharedLibraryFile = file(System.getenv('MDE4CPP_HOME')+"/application/bin/${libraryName}.dll")						
						}
					}
				}	
			}         
		}
	}

    components {
        UMLBenchmark(NativeExecutableSpec)
        {
        	sources {
                cpp {
                    source {
                        srcDirs "src" 
						include	"main.cpp"
                    }
					exportedHeaders {
                        srcDirs ".", System.getenv('MDE4CPP_HOME')+"/application/include", System.getenv('MDE4CPP_HOME')+"/application/include/abstractDataTypes"
                        include "**/*.hpp"
                    }
					lib library: "uml"
					lib library: "ecore"
					lib library: "types"
                }
            }
        }
		
		withType(NativeComponentSpec) {
			binaries.withType(NativeBinarySpec) {
				if (it instanceof NativeExecutableBinarySpec) {
					if(buildType == buildTypes.debug) {
						it.getExecutable().setFile(appendDebugSuffix(it.getExecutable().getFile()))						
					}
				}
			}
        }
    }
	    
    binaries {
	
        all {
        	linker.args '-fopenmp'
        	cppCompiler.args '-std=c++1y','-Wall', '-Wno-overloaded-virtual', '-pipe', '-fopenmp'
			if (buildType == buildTypes.debug) {
				cppCompiler.args '-g'
				
			}
			else {
				cppCompiler.args '-O3'
			}
        }
    }
}
build.finalizedBy(deliverApplication)


task compileUMLBenchmark(dependsOn: 'build') {
	group 'Examples'
	description 'compile UMLBenchmark example'
}
