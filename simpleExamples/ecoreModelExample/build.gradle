plugins {
	id "tui.sse.mde4cpp.mde4cpp-compile-plugin" version "0.2"
}
apply plugin: 'base' 

description 'Build tasks for project: libraryModel_ecore'

task buildLibraryModel_ecore(dependsOn: ['generateLibraryModel_ecore', 'compileLibraryModel_ecore', 'compileApplicationForLibraryModel_ecore']) {
	group 'libraryModel_ecore'
	description 'build libraryModel_ecore'
}

task compileApplicationForLibraryModel_ecore(type: tui.sse.mde4cpp.MDE4CPPCompile) {
	group 'libraryModel_ecore'
	description 'compile ApplicationForlibraryModel_ecore'

	projectFolder = file("./application")
}

task compileLibraryModel_ecore(type: tui.sse.mde4cpp.MDE4CPPCompile) {
	group 'libraryModel_ecore'
	description 'compile libraryModel_ecore'

	projectFolder = file("./src_gen/libraryModel_ecore")
}

task generateLibraryModel_ecore(type: JavaExec) {
	group 'libraryModel_ecore'
	description 'generate C++ code of libraryModel_ecore.ecore model'
	main = "-jar"
	workingDir "./model"
	args System.getenv('MDE4CPP_HOME') + "/application/generator/ecore4CPP.jar", "libraryModel_ecore.ecore", "../src_gen"
	inputs.files file(System.getenv('MDE4CPP_HOME') + "/application/generator/ecore4CPP.jar"), file("model/libraryModel_ecore.ecore")
	outputs.dir file("src_gen")
	
	doFirst {
		if (System.getenv('MDE4CPP_HOME') == null) {
			throw new GradleException('System environment variable "MDE4CPP_HOME" is not defined')
		}
		if (!file(System.getenv('MDE4CPP_HOME') + "/application/generator/ecore4CPP.jar").exists()) {
			throw new GradleException('ecore4CPP.jar is missing in folder ' + file(System.getenv('MDE4CPP_HOME') + '/application/generator') + '. Please run task createEcore4CPP!')
		}
	}
}

compileLibraryModel_ecore.mustRunAfter generateLibraryModel_ecore
compileApplicationForLibraryModel_ecore.mustRunAfter compileLibraryModel_ecore

clean.doFirst {
	def cmakeFolder = file('./src_gen/libraryModel_ecore/.cmake/')
	if(cmakeFolder.exists()) {
    	delete cmakeFolder.absolutePath
   		println "deleting folder " + cmakeFolder.absolutePath
	}
	cmakeFolder = file('./application/.cmake/')
	if(cmakeFolder.exists()) {
    	delete cmakeFolder.absolutePath
   		println "deleting folder " + cmakeFolder.absolutePath
	}
}
