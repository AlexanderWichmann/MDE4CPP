plugins {
	id "tui.sse.mde4cpp.mde4cpp-compile-plugin" version "0.2"
}
apply plugin: 'base' 

description 'Build tasks for project: UmlReflection'

task buildUmlReflection(dependsOn: ['generateUmlReflection', 'compileUmlReflection']) {
	group 'UmlReflection'
	description 'build UmlReflection'
}

task compileUmlReflection(type: tui.sse.mde4cpp.MDE4CPPCompile) {
	group 'UmlReflection'
	description 'compile UmlReflection'

	projectFolder = file("./src_gen/UmlReflection")
}

task generateUmlReflection(type: JavaExec) {
	group 'UmlReflection'
	description 'generate C++ code of UmlReflection.uml model'
	main = "-jar"
	workingDir "./model"
	args System.getenv('MDE4CPP_HOME') + "/application/generator/UML4CPP.jar", "UmlReflection.uml", "../src_gen"
	inputs.files file(System.getenv('MDE4CPP_HOME') + "/application/generator/UML4CPP.jar"), file("model/UmlReflection.uml")
	outputs.dir file("src_gen")

	doFirst {
		if (System.getenv('MDE4CPP_HOME') == null) {
			throw new GradleException('System environment variable "MDE4CPP_HOME" is not defined')
		}
		if (!file(System.getenv('MDE4CPP_HOME') + "/application/generator/UML4CPP.jar").exists()) {
			throw new GradleException('UML4CPP.jar is missing in folder ' + file(System.getenv('MDE4CPP_HOME') + '/application/generator') + '. Please run task createUML4CPP!')
		}
	}
}

compileUmlReflection.mustRunAfter generateUmlReflection

clean.doFirst {
	def cmakeFolder = file('./src_gen/UmlReflection/.cmake/')
	if(cmakeFolder.exists()) {
    	delete cmakeFolder.absolutePath
   		println "deleting folder " + cmakeFolder.absolutePath
	}
}
