plugins {
	id "tui.sse.mde4cpp.mde4cpp-compile-plugin" version "0.1"
}
apply plugin: 'base' 

description 'Build tasks for project: CalcModel'

task buildCalcModel(dependsOn: ['generateCalcModel', 'compileCalcModelAll']) {
	group 'CalcModel'
	description 'build CalcModel'
}

task compileCalcModelAll(dependsOn: ['compileCalcModel', 'compileCalcModelExec']) {
	group 'CalcModel'
	description 'compile the binaries for the fuml generated model CalcModel'
}

task compileCalcModel(type: tui.sse.mde4cpp.MDE4CPPCompile) {
	group 'CalcModel'
	description 'compile CalcModel'

	pathToCMakeList = file("./src_gen/CalcModel")
}

task compileCalcModelExec(type: tui.sse.mde4cpp.MDE4CPPCompile) {
	group 'CalcModel'
	description 'compile CalcModelExec'

	pathToCMakeList = file("./src_gen/CalcModelExec")
}

task generateCalcModel(type: JavaExec) {
	group 'CalcModel'
	description 'generate C++ code of CalcModel.uml model'
	main = "-jar"
	workingDir "./model"
	args System.getenv('MDE4CPP_HOME') + "/application/generator/fUML4CPP.jar", "CalcModel.uml", "../src_gen"
	inputs.files file(System.getenv('MDE4CPP_HOME') + "/application/generator/fUML4CPP.jar"), file("model/CalcModel.uml")
	outputs.dir file("src_gen")
	
	doFirst {
		if (System.getenv('MDE4CPP_HOME') == null) {
			throw new GradleException('System environment variable "MDE4CPP_HOME" is not defined')
		}
		if (!file(System.getenv('MDE4CPP_HOME') + "/application/generator/fUML4CPP.jar").exists()) {
			throw new GradleException('fUML4CPP.jar is missing in folder ' + file(System.getenv('MDE4CPP_HOME') + '/application/generator') + '. Please run task createFUML4CPP!')
		}
	}
}

compileCalcModel.mustRunAfter generateCalcModel
compileCalcModelExec.mustRunAfter compileCalcModel

clean.doFirst {
	def cmakeFolder = file('./src_gen/CalcModel/.cmake/')
	if(cmakeFolder.exists()) {
    	delete cmakeFolder.absolutePath
   		println "deleting folder " + cmakeFolder.absolutePath
	}

	def cmakeFolderExec = file('./src_gen/CalcModelExec/.cmake/')
	if(cmakeFolderExec.exists()) {
    	delete cmakeFolderExec.absolutePath
   		println "deleting folder " + cmakeFolderExec.absolutePath
	}
}
