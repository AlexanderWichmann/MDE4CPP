apply plugin: 'base' 

description 'Build tasks for project: fUML'

task buildFUML(dependsOn: ['generateFUML', 'compileFUML']) {
	group 'fUML'
	description 'build fUML in debug and release'
}
task buildFUMLDebug(dependsOn: ['generateFUML', 'compileFUMLDebug']) {
	group 'fUML'
	description 'build fUML in debug'
}
task buildFUMLRelease(dependsOn: ['generateFUML', 'compileFUMLRelease']) {
	group 'fUML'
	description 'build fUML in release'
}

task compileFUML(dependsOn: ['compileFUMLDebug', 'compileFUMLRelease']) {
	group 'fUML'
	description 'compile fUML in debug and release'
}
task compileFUMLDebug {
	group 'fUML'
	description 'compile fUML in Debug'

	def folder = new File(file('./src_gen/fUML/.cmake/Debug').absolutePath)
	if( !folder.exists() ) {
		folder.mkdirs()
	}

	doLast {
		exec {
			workingDir 'src_gen/fUML/.cmake/Debug'
			commandLine 'cmd', '/c', 'cmake -G "MinGW Makefiles" -D CMAKE_BUILD_TYPE=Debug ' + file("./src_gen/fUML").absolutePath
		}
		exec {
			workingDir 'src_gen/fUML/.cmake/Debug'
			if (make_parallel_jobs)
			{			
				commandLine 'cmd', '/c', 'mingw32-make install -j' + make_parallel_jobs
			}
			else
			{
				commandLine 'cmd', '/c', 'mingw32-make install' 
			}
		}
	}
}
task compileFUMLRelease {
	group 'fUML'
	description 'compile fUML in Release'

	def folder = new File(file('./src_gen/fUML/.cmake/Release').absolutePath)
	if( !folder.exists() ) {
		folder.mkdirs()
	}

	doLast {
		exec {
			workingDir 'src_gen/fUML/.cmake/Release'
			commandLine 'cmd', '/c', 'cmake -G "MinGW Makefiles" -D CMAKE_BUILD_TYPE=Release ' + file("./src_gen/fUML").absolutePath
		}
		exec {
			workingDir 'src_gen/fUML/.cmake/Release'
			if (make_parallel_jobs)
			{			
				commandLine 'cmd', '/c', 'mingw32-make install -j' + make_parallel_jobs
			}
			else
			{
				commandLine 'cmd', '/c', 'mingw32-make install' 
			}
		}
	}
}

task generateFUML(type: JavaExec) {
	group 'fUML'
	description 'generate C++ code of fUML.ecore model'
	main = "-jar"
	workingDir "./model"
	args System.getenv('MDE4CPP_HOME') + "/application/generator/ecore4CPP.jar", "fUML.ecore", "../src_gen"
	inputs.files file(System.getenv('MDE4CPP_HOME') + "/application/generator/ecore4CPP.jar"), file("model/fUML.ecore")
	outputs.dir file("src_gen")
	
	doFirst {
		if (System.getenv('MDE4CPP_HOME') == null) {
			throw new GradleException('System environment variable "MDE4CPP_HOME" is not defined')
		}
		if (!file(System.getenv('MDE4CPP_HOME') + "/application/generator/ecore4CPP.jar").exists()) {
			throw new GradleException('ecore4CPP.jar is missing in folder ' + file(System.getenv('MDE4CPP_HOME') + '/application/generator') + '. Please run task createEcore4CPP!')
		}
	}
}

compileFUML.mustRunAfter generateFUML
compileFUMLDebug.mustRunAfter generateFUML
compileFUMLRelease.mustRunAfter generateFUML

clean.doFirst {
	def cmakeFolder = file('./src_gen/fUML/.cmake/')
	if(cmakeFolder.exists()) {
    	delete cmakeFolder.absolutePath
   		println "deleting folder " + cmakeFolder.absolutePath
	}
}
